! -*- mode: F90 ; mode: font-lock ; column-number-mode: true ; vc-back-end: Git -*-
!=============================================================================!
!                          R A N D O M                                        !
!=============================================================================!
! Random number generation. Can be modified to act as a wrapper to your RNG   !
! choice. Here I'm just using the intrinsic Fortran90 RANDOM_NUMBER function. !
!=============================================================================!
Module Random

  Use Constants , Only : dp
  Implicit None                                 !Impose strong typing

  Private                                       !Everything is private ...

  !---------------------------------------------------------------------------!
  !                       P u b l i c   R o u t i n e s                       !
  !---------------------------------------------------------------------------!
  Public :: random_set_random_seed               !... unless exposed here.
  Public :: random_uniform_random
  Public :: random_test_uniform

  !---------------------------------------------------------------------------!
  !                        P u b l i c   V a r i a b l e s                    !
  !---------------------------------------------------------------------------!
  Public :: jumble 
  integer,dimension(1:4) :: jumble

  !---------------------------------------------------------------------------!
  !                      P r i v a t e   V a r i a b l e s                    !
  !---------------------------------------------------------------------------!

  !For the uniform random number generator, we presume a 32-bit integer in order
  !to do bit-shuffling, so define and store here.
  Integer, Parameter        :: bit_32=Selected_int_kind(9)
  Integer(kind=bit_32),Save :: ix,iy                     !intermediate randoms

  Logical,Save              :: random_initialised=.False.

Contains


  subroutine Random_set_random_seed(seed,dumrank,dumsize) 
    !=========================================================================!
    ! Set internal random number generator seed as per GNU fortran manual.    !
    ! Use seed = 0 to initialise the RNG in its default state, otherwise      !
    ! seeded from the clock.                                                  !
    !-------------------------------------------------------------------------!
    ! D. Quigley, August 2018                                                 !
    !=========================================================================!
    implicit none
    integer,intent(in) :: seed
    integer,intent(in),optional  :: dumrank 
    integer,intent(in),optional  :: dumsize  
    integer :: i, n, clock, ierr
    integer,dimension(:),allocatable :: seed_array

    !comms variables
    integer :: myrank=0,size=1

    if (present(dumrank)) myrank = dumrank
    if (present(dumsize)) size   = dumsize

    ! If the RNG hasn't already been seeded...
    if (.not.random_initialised) then

       ! https://gcc.gnu.org/onlinedocs/gcc-4.5.1/gfortran/RANDOM_005fSEED.html
       if (seed == 0 ) then
          call random_seed()
       else

          call random_seed(size = N)
          allocate(seed_array(1:n),stat=ierr)
          if (ierr/=0) stop 'Error allocating random seed array in random.f90'

          call system_clock(count=clock)
          clock = clock + int(myrank*1000)
          seed_array = clock + 37 * (/ (i - 1, i = 1, N) /)
          call random_seed(put=seed_array)

          deallocate(seed_array)

       end if

       random_initialised = .true.                         ! it has now

    end if

    return

  end subroutine random_set_random_seed

  function random_uniform_random() 
    !=========================================================================!
    ! Return a single random deviate ~ uniform [0,1].                         !
    !-------------------------------------------------------------------------!
    ! D. Quigley, August 2018                                                 !
    !=========================================================================!
    implicit none
    real(kind=dp)                 :: Random_uniform_random
    real(kind=dp) :: x

    call random_number(x)
    random_uniform_random = x

    return

  end function random_uniform_random


  subroutine random_test_uniform(log)
    !=========================================================================!
    ! Tests the distribution of random numbers generated by the above         !
    ! random_uniform_random routine.                                          !
    !-------------------------------------------------------------------------!
    ! D.Quigley - April 2011                                                  !
    !=========================================================================!
    implicit none
    integer,parameter :: ntest = 1000000
    integer,parameter :: nbins = 100

    integer,intent(in) :: log  ! unit number to report results to

    integer,dimension(nbins) :: histogram
    integer :: itest,k
    real(kind=dp) :: xi

    write(log,'("#                                                              #")')   
    write(log,'("# Testing random number generator with ",I12,"   samples  #")')ntest
    write(log,'("# -------------------------------------------------------------#")')
    write(log,'("#                                                              #")') 

    histogram = 0

    do itest = 1,ntest

       xi = random_uniform_random()
       k  = int(xi*real(nbins,kind=dp)) + 1
       histogram(k) = histogram(k) + 1

    end do

    write(log,'("# Ntrials / Nbins            = ",I12,10x" samples  #")')int(real(ntest,kind=dp)/real(nbins,kind=dp))
    write(log,'("# Min hits per histogram bin = ",I12,10x" samples  #")')minval(histogram,1)
    write(log,'("# Max hits per histogram bin = ",I12,10x" samples  #")')maxval(histogram,1)
    write(log,'("#                                                              #")') 

    return

  end subroutine random_test_uniform

End Module random
